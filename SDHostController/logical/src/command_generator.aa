// Generating 48 bit command frame
// Also storing the response from the SD card into the Response registers
// Returns Interrupt statuses
$module [command_generator] $in() $out(normalInterruptStatusVar1:$uint<8> normalInterruptStatusVar2:$uint<8> errorInterruptStatusVar1:$uint<8> errorInterruptStatusVar2:$uint<8> dataErrorInterruptStatusVar1:$uint<8> dataTransferComplete:$uint<8>) $is
{
	$branchblock[command]
	{
	dataRxEnrty1:= ZERO_1
	dataRxEnrty2:= ZERO_1 
	$call readSDHCRegisters (Command) (commandRegVal)
	$call readSDHCRegisters ({Command + 1}) (commandRegVal1)
	$volatile tempVal := ($concat ZERO_32 ZERO_1 ONE_1 ($slice commandRegVal1 5 0))
	$merge $entry loopcommand40bits
		$phi C := ($bitcast ($uint<8>)3) $on $entry next_C $on loopcommand40bits
		$phi newCommand40bits := tempVal $on $entry command40bits $on loopcommand40bits
	$endmerge
		$call readSDHCRegisters ((Argument + C)) (tempArgument)
		$volatile command40bits := (($slice newCommand40bits 31 0) && tempArgument)
		$volatile next_C := (C - 1)
	$if(C!=0)$then
		$place[loopcommand40bits]
	$endif
	$call generate_crc_7 (command40bits) (crc7)

	$volatile commandbits := ($concat command40bits crc7 ONE_1)
	//Sending the commands to the Command transmission pipe
	//The output enable is one 
	oe := ONE_1
	$branchblock[send_command]
	{
		$dopipeline $depth 7 $fullrate
		$merge $entry $loopback
			$phi J:= ZERO_8 $on $entry next_J $on $loopback
			$phi old_commandbits:= commandbits $on $entry new_commandbits $on $loopback
		$endmerge
			$volatile next_J:= (J + 1)
			$volatile bit:= ($slice old_commandbits 47 47)
			commandTx:= bit
			$volatile new_commandbits:= (old_commandbits << 1) 
		$while(next_J < 48)
	}
	// The output enable is held low
	oe := ZERO_1
	//Recieving response from the SD card
	response_type := ($slice commandRegVal 1 0)

	//-------------Data present or not checked---------------------- 
	dataPresentSelect:= (commandRegVal [] 5)
	$if(dataPresentSelect == 1)$then
		$call readSDHCRegisters (Transfer) (transferVal)
		txOrRX:= ($bitreduce | (transferVal & _h10))// 1 - Read , 0 - Write
		$merge $entry loopPresent
			$phi E:= ONE_8 $on $entry next_E $on loopPresent
			$phi readNewPresent := ($bitcast ($uint<16>)0) $on $entry readPresentState $on loopPresent
		$endmerge
			$call readSDHCRegisters ((PresentState + E)) (readPresentStateTemp)
			$volatile readPresentState := (($slice readNewPresent 7 0) && readPresentStateTemp )
			$volatile next_E := (E-1)
		$if(E != 0)$then
			$place[loopPresent]
		$endif
		$volatile presentState0Var := ($slice readPresentState 7 0)
		$volatile presentState1Var := ($slice readPresentState 15 8)
		//------------Read or Write Transfer Active-----------
		$if(response_type != 3)$then
			$volatile tempPstate := ($mux txOrRX ( presentState1Var| _h02) (presentState1Var | _h01))
			$call writeSDHCRegisters({PresentState + 1} tempPstate) ()
		$endif
		//-------------DAT Line Active and Command Inhibit (DAT)------------------
		$call writeSDHCRegisters (PresentState (presentState0Var | _h06)) ()	
	$endif
	//-------------R1,R6,R7 Response Length 48----------------------
	$if ((response_type == 2) | (response_type == 3)) $then //response_type 3 R1b need to check for busy
		//Enable the response
		responseEnable:= ONE_1
		$if(response_type == 3)$then
			busyResponse := ONE_1
		$endif
		$branchblock[receive_response]
		{
		$dopipeline $depth 5 $fullrate
		$merge $entry $loopback
			$phi J:= ($bitcast ($uint<6>) 0) $on $entry next_J $on $loopback
			$phi old_responsebits:= ($bitcast ($uint<48>) 0) $on $entry new_responsebits $on $loopback
		$endmerge
			$volatile next_J:= (J + 1)
			responsebit:= responseRx
			$volatile responsebits:= ($concat ($bitcast ($uint<47>) 0) responsebit)
			$volatile new_responsebits:= ((old_responsebits<<1) | responsebits)
		$while(next_J <48)
		}(new_responsebits=>response)
		//Disable the response
		responseEnable := ZERO_1
		
		$if(response_type == 3)$then
			//Wait for busy signal to end 
			//busy := ONE_1
			$merge $entry waitloop $endmerge
				wait:= DAT0Rx
			$if(wait == ZERO_1)$then
				$place[waitloop]
			$endif
			busyResponse := ZERO_1
		$endif

		$if ((commandRegVal & _h8) == 8) $then  //CRC Check Enable
			$call generate_crc_7 (($slice response 47 8)) (R1_6_7CRC)
			//This will assert the CommandCRCError Interrupt Status.
		    CommandCRCError := ($mux (($slice response 7 1) == R1_6_7CRC) _b00000000 _b00000010) 
		$endif
		$if ((commandRegVal & _h10) == 16) $then //Command Index Enable
			commandIndexCheck := (($slice commandRegVal1 5 0) == ($slice response 45 40))
			//This will assert the CommandIndexError Interrupt Status.
			CommandIndexError:= ($mux commandIndexCheck _b00000000 _b00001000)
		$endif
		
		$merge $entry loopResponse
			$phi R := ZERO_8 $on $entry next_R $on loopResponse
			$phi tempResponse := ($slice response 39 8) $on $entry nextResponse $on loopResponse
		$endmerge
			$call writeSDHCRegisters ((Response0 + R) ($slice tempResponse 7 0)) ()
			$volatile nextResponse := (ZERO_8 && ($slice tempResponse 31 8))
			$volatile next_R := (R + 1)
		$if(next_R < 4)$then 
			$place[loopResponse]
		$endif
		//Present State Change
		$call readSDHCRegisters (PresentState) (tempPresentState2)
		$volatile presentState2 := ((tempPresentState2>>1) << 1)
		//Normal Interrupt Variable
		$call readSDHCRegisters (NormalInterruptStatus) (normalInterrupt2)
		$call writeSDHCRegisters (PresentState presentState2) ()//Clearing commandInhibit(CMD)
		//for response_type 3 we have to clear commandInhibit_DAT
		//Command End Bit Error check
		commandEndBitError := ($mux (response [] 0) ZERO_8 ($bitcast ($uint<8>) 4))
		normalInterruptStatusVar_2:= (normalInterrupt2 | _h01)
		errorInterruptStatusVar_2 := ($reduce | CommandIndexError CommandCRCError commandEndBitError)
		$place[normalInterruptResponse2]
	

	$else $if(response_type == 1) $then //R2 Response Length 136
		responseEnable := ONE_1
		bigResponseEnable := ONE_1
		$branchblock[receive_bigresponse]
		{
		$dopipeline $depth 10 $fullrate
		$merge $entry $loopback
			$phi J:= ($bitcast ($uint<8>) 0) $on $entry next_J $on $loopback
			$phi old_bigresponsebits:= ($bitcast ($uint<136>) 0) $on $entry new_bigresponsebits $on $loopback
		$endmerge
			$volatile next_J:= (J + 1)
			bigresponsebit:= responseRx
			$volatile bigresponsebits:= ($concat ($bitcast ($uint<135>) 0) bigresponsebit)
			$volatile new_bigresponsebits:= ((old_bigresponsebits<<1) | bigresponsebits)
		$while(next_J < 136)
		}(new_bigresponsebits=>bigResponse)
		responseEnable := ZERO_1
		bigResponseEnable := ZERO_1
		$rreport(gen_comm f bigResponse)

		$volatile CID_CRC := ($slice bigResponse 7 1)
		$if ((commandRegVal & _h8) == 8) $then  //CRC Check Enable
			$call generate_crc_7_120 (($slice bigResponse 127 8)) (R2_CRC) //-----CRC FOR 120bits------
			//This will assert the CommandCRCError Interrupt Status.
			CommandCRCError1 := ($mux (CID_CRC == R2_CRC) _b00000000 _b00000010)
		$endif
		commandEndBitError1:= ($mux (bigResponse [] 0) ZERO_8 ($bitcast ($uint<8>) 4))
		errorInterruptStatusVar_3 := (CommandCRCError1 | commandEndBitError1)

		$branchblock[big_resp]
		{
			$dopipeline $depth 10 $fullrate
			$merge $entry $loopback
				$phi k := ($bitcast ($uint<8>) 0) $on $entry next_k $on $loopback
				$phi temp := ($slice bigResponse 127 8) $on $entry next_temp $on $loopback 
			$endmerge
				$volatile address:= (16 + k)//Response0 + k
				$call writeSDHCRegisters (address ($slice temp 7 0)) ()
				$volatile next_temp:= (temp >> 8)
				$volatile next_k:= ( k + 1)
			$while(next_k < 15)
		}

		$call readSDHCRegisters (PresentState) (tempPresentState3)
		presentState3:= ((tempPresentState3>>1) << 1)
		$call readSDHCRegisters (NormalInterruptStatus) (normalInterrupt3)
		$call writeSDHCRegisters (PresentState presentState3) ()//Clearing commandInhibit(CMD)
		normalInterruptStatusVar_3:= (normalInterrupt3| _h01)//command Interrupt
		$place[normalInterruptResponse3] 
		
		$else $if(response_type == 0) $then //No response type
				$call readSDHCRegisters (PresentState) (tempPresentState1)
				$volatile presentState1:= ((tempPresentState1>>1) << 1)
				$call readSDHCRegisters (NormalInterruptStatus) (normalInterrupt1)
				$call writeSDHCRegisters (PresentState presentState1) ()//Clearing commandInhibit(CMD)
				$volatile normalInterruptStatusVar_1:= (normalInterrupt1| _h01)
				$volatile errorInterruptStatusVar_1 := ZERO_8
				$place[normalInterruptResponse1] 
			$endif
		$endif
	$endif	
	
	$merge normalInterruptResponse1 normalInterruptResponse2 normalInterruptResponse3
		$phi normalInterruptStatusVar1 := normalInterruptStatusVar_1 $on normalInterruptResponse1
										normalInterruptStatusVar_2 $on normalInterruptResponse2
										normalInterruptStatusVar_3 $on normalInterruptResponse3 	
		$phi errorInterruptStatusVar1 := errorInterruptStatusVar_1 $on normalInterruptResponse1
										errorInterruptStatusVar_2 $on normalInterruptResponse2
										errorInterruptStatusVar_3 $on normalInterruptResponse3 	
	$endmerge
	$if(dataPresentSelect == 1)$then
	//Receive Data from the SDCard
		$if((txOrRX == 1) & (response_type !=3))$then
			$call readSDHCRegisters (Transfer) (tempTransfer)
			singleMultipleReadBlock:= ($bitreduce | (tempTransfer & _h20))//1:multipleblock 0: single block
			$call dataRx (singleMultipleReadBlock) (dataErrorInterruptStatusVar1)
			dataRxEnrty1:= ONE_1
			dataRxEnrty2:= ZERO_1
			transferCompleteData:= ($mux singleMultipleReadBlock ($bitcast ($uint<8>)2) ZERO_8)
			$if(singleMultipleReadBlock ==1)$then
				$merge $entry loopBlock1
					$phi B:= ONE_8 $on $entry next_B $on loopBlock1
					$phi readNewBlockCount := ($bitcast ($uint<16>)0) $on $entry readBlockCount $on loopBlock1
				$endmerge
					$call readSDHCRegisters ((BlockCount + B)) (readnewBlockCountTemp)
					$volatile readBlockCount := (($slice readNewBlockCount 7 0) && readnewBlockCountTemp )
					$volatile next_B := (B-1)
				$if(B != 0)$then
					$place[loopBlock1]
				$endif
				newReadBlockCount:= (readBlockCount -1)
				$call writeSDHCRegisters ({BlockCount + 1} ($slice newReadBlockCount 15 8)) ()
				$call writeSDHCRegisters (BlockCount ($slice newReadBlockCount 7 0)) ()
			$endif
		$endif
		dataTransferComplete:= ($mux (response_type == 3) ($bitcast ($uint<8>)2) transferCompleteData)
	$endif
	normalInterruptStatusVar2:= ZERO_8
	errorInterruptStatusVar2:= ZERO_8
	normalInterruptFlag1:= _hFF
	normalInterruptFlag2:= _hFF
	errorInterruptFlag1 := _hFF
	errorInterruptFlag2 := _hFF
	}
}

